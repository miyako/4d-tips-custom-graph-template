<!--#4DCODE

$svg:="<g style='clip-path: url(#ID_backgroundClipPath)'>"

$total:=0
$pi:=Pi

$centerX:=($graphX1+$graphX2)/2
$centerY:=($graphY1+$graphY2)/2
$radiusX:=($graphX2-$graphX1)/2-(2*20)
$radiusY:=($graphY2-$graphY1)/2-(2*20)

if($pieDirection=1)
	$startAngle:=$startAngle-90 // for internal calculation, do NOT change this line
Else
	$startAngle:=$startAngle+90 // for internal calculation, do NOT change this line
End if

If ($radiusX<$radiusY)
	$radius:=$radiusX
Else 
	$radius:=$radiusY
End if 

ARRAY REAL($_anglesDeg;$NbValues)
ARRAY REAL($_anglesRad;$NbValues)
ARRAY TEXT($_textPercentage;$NbValues)
ARRAY REAL($_PieValues;$NbValues)
COPY ARRAY($yValuesArrPtr{1}->;$_PieValues)

For ($value;1;$NbValues)
	$_PieValues{$value}:=Abs($_PieValues{$value})
End for 
$total:=Sum($_PieValues)

For ($value;1;$NbValues)
	$_anglesDeg{$value}:=360*$_PieValues{$value}/$total
	$_anglesRad{$value}:=$_anglesDeg{$value}*2*$pi/360
	$_textPercentage{$value}:=String(Round(100*$_PieValues{$value}/$total;2))+" %"
End for 

For ($i;1;3)  //First for SHADOWS, second for COLOURED bars, third labels %
	
	$SumAngle:=$startAngle
	For ($value;1;$NbValues)
		
		Case of 
			: ($i=1)
				$svg:=$svg+"<g fill='black' fill-opacity='0.2' id='ID_graph_shadow_"+String($value)+"'>"
			: ($i=2)
				$svg:=$svg+"<g fill='"+$barColors{$value}+"' filter='none' id='ID_bar_"+String($value)+"' opacity='1' stroke='none' stroke-opacity='1' stroke-width='0'>"
			: ($i=3)
				$svg:=$svg+"<g fill='"+$barColors{$value}+"' filter='none' id='ID_pie_label_"+String($value)+"' opacity='1' stroke='none' stroke-opacity='1' stroke-width='0'>"
		End case 
		
		  //***
		If ($pieDirection=1)
			$rotateAngle:=$SumAngle
			$translateAngleRad:=($rotateAngle+($_anglesDeg{$value}/2))*2*$pi/360
		Else 
			$rotateAngle:=$SumAngle*(-1)
			$translateAngleRad:=($rotateAngle-($_anglesDeg{$value}/2))*2*$pi/360
		End if 
		  //***
		
		If ($i<3)
			
			$path:=""
			$path:=$path+"M"+String($centerX;"&xml")+","+String($centerY;"&xml")+" l"+String($radius;"&xml")+",0 "
			$path:=$path+"A"+String($radius;"&xml")+","+String($radius;"&xml")+" 0 "
			
			If ($_anglesDeg{$value}#360)
				If ($_anglesDeg{$value}<180)
					$path:=$path+"0,0 "
				Else 
					$path:=$path+"1,0 "
				End if 
			Else 
				$path:=$path+"1,1 "
			End if 
			
			$path:=$path+String(Round($centerX+($radius*Cos($_anglesRad{$value}));2);"&xml")+","
			$path:=$path+String(Round($centerY-($radius*Sin($_anglesRad{$value}));2);"&xml")+" z"
			
			
			If ($pieDirection=1)
				$rotateAngle:=$SumAngle+$_anglesDeg{$value}
			Else 
				$rotateAngle:=$SumAngle*(-1)
			End if 
			
			  //***
			$rotatePie:="rotate("+String(Round($rotateAngle;2);"&xml")+","+String($centerX;"&xml")+","+String($centerY;"&xml")+")"
			  //***
			
			$translatePieX:=$pieShift*Cos($translateAngleRad)
			If $i=1
				$translatePieX:=$translatePieX+$BarShadowX
			End if 
			$translatePieY:=$pieShift*Sin($translateAngleRad)
			If $i=1
				$translatePieY:=$translatePieY+$BarShadowX
			End if 
			$translatePie:="translate("+String(Round($translatePieX;2);"&xml")+","
			$translatePie:=$translatePie+String(Round($translatePieY;2);"&xml")+")"
			
			$svg:=$svg+"<g transform='"+$translatePie+" "+$rotatePie+"'>"
			If ($i=1)
				$svg:=$svg+"<path d='"+$path+"'/>"
			Else 
				$svg:=$svg+"<path d='"+$path+"'/>"
				$svg:=$svg+"<path d='"+$path+"' fill='url(#WhiteBlackGradient)'/>"
				$svg:=$svg+"<path d='"+$path+"' fill-opacity='0.3' filter='url(#SaturateMinus)'/>"
			End if 
			$svg:=$svg+"</g>"
			
		Else 
			
			$translateTextX:=50+(($pieShift+(7/10*$radius))*Cos($translateAngleRad))
			$translateTextY:=20+(($pieShift+(7/10*$radius))*Sin($translateAngleRad))
			$translateText:="translate("+String(Round($translateTextX;2);"&xml")+","
			$translateText:=$translateText+String(Round($translateTextY;2);"&xml")+")"
			
			$svg:=$svg+"<g transform='"+$translateText+"' font-size='"+String($pieFontSize;"&xml")+"'>"
			$svg:=$svg+"<text fill='grey' text-anchor='middle' font-weight='bold' x='"+String($centerX-49;"&xml")+"' y='"+String($centerY-($pieFontSize/2)-1;"&xml")+"'>"+$_textPercentage{$value}+"</text>"
			$svg:=$svg+"<text fill='white' text-anchor='middle' font-weight='bold' x='"+String($centerX-50;"&xml")+"' y='"+String($centerY-($pieFontSize/2)-2;"&xml")+"'>"+$_textPercentage{$value}+"</text>"
			
			$svg:=$svg+"</g>"
			
		End if 
		$svg:=$svg+"</g>"
		
		$SumAngle:=$SumAngle+$_anglesDeg{$value}
	End for 
End for 

$svg:=$svg+"</g>"  // END CLIPPATH
$svg:=Replace string($svg;"4DEVAL";"xxxx")

-->

<!--4DHTML $svg-->
	
</g>
</svg>
